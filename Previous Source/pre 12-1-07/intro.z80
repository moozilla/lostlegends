.nolist
#include "ti83plus.inc"
.list
.org	$9D93	; 9D95-2 because of following db
	.db	t2ByteTok, tAsmCmp

	res TextInverse,(iy+TextFlags)
	b_call(_RunIndicOff)

	in a,(06)		;get page?
	ld hl,FontHook
	b_call($4fe4)		;enable font hook

        b_call(_GrBufClr)
	ld hl, scrn_title
	ld de, PlotSScreen+(3*12)	;Start at 3rd row of display
	ld bc, 49*12			;49 rows of data
	ldir
	b_call(_GrBufCpy)

	ld b,0				;initial menu selection
	jr intromenu
tmenuitem:
	ld a,b
	xor 1
	ld b,a
	call toggleinverse
intromenu:
	call toggleinverse

	ld hl,$370d
	ld (PenCol),hl
	ld hl,str_title1
	b_call(_VPutS)

	call toggleinverse

	ld hl,$372c
	ld (PenCol),hl
	ld hl,str_title2
	b_call(_VPutS)

kloop:
	b_call(_GetCSC)
	cp skLeft
	jr z,tmenuitem
	cp skRight
	jr z,tmenuitem
	cp skEnter
	jr nz,kloop

	dec b
	jr z,ending	;continue chosen

        b_call(_GrBufClr)
	ld hl, scrn_intro
	ld de, PlotSScreen+(22*12)	;Start at x=22
	ld bc, 35*12			;35 rows of data
	ldir
	ld hl,str_intro
	ld de,$040A
	call scrolltext
	jp exit

ending:
	res TextInverse,(iy+TextFlags)
        b_call(_GrBufClr)
	ld hl, scrn_end1
	ld de, PlotSScreen+(1*12)
	ld bc, 24*12
	ldir
	ld hl, scrn_end2
	ld de, PlotSScreen+(45*12)
	ld bc, 18*12
	ldir
	ld hl,str_end
	ld de,$1C0A
	call scrolltext

        b_call(_GrBufClr)
	ld hl, scrn_end_1
	ld de, PlotSScreen+(16*12)
	ld bc, 48*12
	ldir
	b_call(_GrBufCpy)
	ld hl,$0501
	ld (PenCol),hl
	ld hl,str_end_1
	b_call(_VPutS)

	ld b,200	;delay a bit
delay1:
	halt		;1/20 second
	halt
	djnz delay1

        b_call(_GrBufClr)
	ld hl, scrn_end_2
	ld de, PlotSScreen+(32*12)
	ld bc, 29*12
	ldir
	b_call(_GrBufCpy)
	ld de,$0E02
	ld (PenCol),de
	ld hl,str_end_2
	b_call(_VPutS)
	ld de,$0E29
	ld (PenCol),de
	b_call(_VPutS)
	ld de,$0E4b
	ld (PenCol),de
	b_call(_VPutS)

	ld b,200	;delay a bit
delay2:
	halt		;1/20 second
	halt
	djnz delay2

        b_call(_GrBufClr)
	ld hl, scrn_end_3
	ld de, PlotSScreen+(1*12)
	ld bc, 52*12
	ldir
	b_call(_GrBufCpy)
pauseloop:
	b_call(_GetCSC)
	or a
	jr z,pauseloop
exit:
	b_call(_GrBufClr)
	b_call($4fe7)		;disable font hook
	res TextInverse,(iy+TextFlags)
	ret

scrolltext:		;text is in hl, top coords in de
	push hl
	push de
reset:
	b_call(_GrBufCpy)
	pop de
	ld (PenCol),de
	pop hl
	ld a,(hl)
	dec a
	ret z
	b_call(_VPutS)
	ld a,(hl)
	dec a		;test if 1, then only 1 line
	jr nz, nres
	inc hl
	jr pause
nres:
	ld a,8
	add a,d
	ld d,a
	ld (PenCol),de
	sub 8		;restore de
	ld d,a
	b_call(_VPutS)
pause:
	push hl
	push de
	ld b,200	;delay a bit
delay:
	halt		;1/20 second
	djnz delay
	jr reset

toggleinverse:
	ld hl,Flags+TextFlags
	ld a,(hl)
	xor 1<<TextInverse
	ld (hl),a
	ret

#include "text.z80"
#include "font.z80"
#include "screens.z80"

.end
.end