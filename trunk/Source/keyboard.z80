keygettop:
;keyget
;by Jason Lee (Liazon) and modified by Galandros
;=============================================
;for convenience, returns only y=, 2nd, mode, del, and arrows in the right spots
;other F2-5 keys are ignored and masked out

;the number of nops right now are for SE convenience
;subject to change. also might change to more efficient ways of wasting time without wasting space
;done. From 60 bytes to 45 bytes! preserves ix and tested! - Galandros
;delay between port use: 23 to 24 clocks

;returns: a = byte holding bits corresponding to down, left, right, up, y=, 2nd, mode, del
keyget:
	push hl
keygetloop:
	ld a,$FF		;reset keyboard
	out (1),a
	push af
	ld a,(bc)
	ld a,diGroup7
	out (1),a
	ld hl,gs_keymem
	in a,(1)
	and $F0			;protect the bits with y=, 2nd, mode, del; destroy others
	ld (gs_keymem),a		;keep a duplicate
	pop af			;$FF popped
;	ld a,$FF
	out (1),a
;16 clocks delay
	inc (hl)
	dec (hl)
	dec a				;a = $FE = diKeyRowPad
;	ld a,diKeyRowPad		;the same as $FF-$01
	out (1),a
	inc (hl)
	dec (hl)
	nop
;24 clocks delay
	in a,(1)
	and $0F			;unused bits are set
	or (hl)			;put the two bytes together
	ld (hl),a
	cp $FF			;need compare, inc doesn't affect carry
	jp z,keygetloop		;keys are on by default (0 means key pressed, 1 means not)
	pop hl
	ret
keygetbottom:
#define keyboardz80code keygetbottom - keygettop
#define keyboardz80data 0
#define keyboardz80size keyboardz80code+keyboardz80data
 print_sizes("keyboard.z80", keyboardz80size, keyboardz80data, keyboardz80code)
